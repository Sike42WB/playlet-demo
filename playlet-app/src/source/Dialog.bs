function ShowErrorDialog(message as object, bulletText as object, bottomMessage as object)
    ' Note: dialog can be null since StandardMessageDialog is only available on 10.0 and later
    dialog = CreateObject("roSGNode", "StandardMessageDialog")
    SetPalette(dialog)
    dialog.message = message
    dialog.bulletText = bulletText
    dialog.bottomMessage = bottomMessage
    dialog.title = "Error loading Playlet"
    dialog.buttons = ["Exit"]

    ' bs:disable-next-line NO_OBSERVE_STRING_CALLBACK
    dialog.observeField("buttonSelected", "OnButtonSelected")
    ' bs:disable-next-line NO_OBSERVE_STRING_CALLBACK
    dialog.observeField("wasClosed", "OnDialogClosed")

    m.top.getScene().dialog = dialog
end function

function SetPalette(dialog as object)
    palette = createObject("roSGNode", "RSGPalette")
    palette.colors = {
        DialogBackgroundColor: "#242424FF"
        DialogFocusColor: "#CECECEFF"
        DialogFocusItemColor: "#202020FF"
        DialogSecondaryTextColor: "#F8F8F8FF"
        DialogSecondaryItemColor: "0xFF1C30FF"
        DialogTextColor: "#EEEEEEFF"
    }
    dialog.palette = palette
end function

function OnButtonSelected()
    m.top.getScene().dialog.close = true
end function

function OnDialogClosed()
    ' TODO:P1 throw an exception to cause a crash report to be generated
    ' Should include which playlet lib failed to load
    m.top.getScene().exitChannel = true
end function
