import "pkg:/components/EcpArgs.bs"
import "pkg:/components/parts/AutoBind/AutoBind.part.bs"
import "pkg:/components/parts/AutoBind/OnNodeReadyNoOp.bs"
import "pkg:/source/utils/LoadingScreen.bs"
import "pkg:/source/utils/Locale.bs"

function Init()
    m.scene = m.top.getScene()
    ' At this point, the "MainScene" node is not yet added to the scene, and does not have a parent yet.
    ' Let's wait until it has one.
    m.MainSceneContainer = m.scene.findNode("MainSceneContainer")
    m.MainSceneContainer.ObserveField("change", FuncName(MainSceneContainerChanged))
end function

function MainSceneContainerChanged()
    AutoBindSceneGraph()
    StartWebServer()
    HideLoadingScreen()
    InitEcpArgs()
    m.scene.signalBeacon("AppLaunchComplete")
    CopyLoadingMessagesToCache()
    ShowAnnouncement()
end function

function StartWebServer()
    m.loungeService = m.top.findNode("LoungeService")
    m.loungeService@.StartService()

    m.webServer = m.top.findNode("WebServer")
    m.webServer@.StartServer()

    m.dialServer = m.top.findNode("DialServer")
    m.dialServer@.StartServer()
end function

function ShowAnnouncement()
    title = "Announcement"

    message = [
        "Hello again!",
        "Invidious servers are actively getting blocked by YouTube.",
        `You might see the error message "This helps protect our community. Learn more" when playing a video.`,
        "This is a known issue and the Invidious dev team is working to fix it."
        "See more information at https://github.com/iv-org/invidious/issues/4734"
        "We apologize for the inconvenience."
    ]

    buttons = [Tr(Locale.Buttons.OK)]

    dialog = CreateObject("roSGNode", "SimpleDialog")
    dialog.title = title
    dialog.message = message
    dialog.buttons = buttons
    deviceInfo = CreateObject("roDeviceInfo")
    displaySize = deviceInfo.GetDisplaySize()
    dialog.width = displaySize.w - 180

    m.top.getScene().dialog = dialog
end function
