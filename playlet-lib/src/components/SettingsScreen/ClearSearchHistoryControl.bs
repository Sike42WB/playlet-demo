import "pkg:/source/services/SearchHistory.bs"

function Init()
    m.button = m.top.findNode("clearButton")
    m.button.observeField("buttonSelected", "OnButtonSelected")
    m.top.observeField("focusedChild", "OnFocusChanged")
end function

function OnFocusChanged()
    focused = m.top.hasFocus()
    if focused
        m.button.SetFocus(true)
    end if
end function

function OnButtonSelected()
    SearchHistory.Clear()
    SearchClearedDialog()
end function

function SearchClearedDialog()
    dialog = CreateObject("roSGNode", "StandardMessageDialog")
    dialog.title = "Success"
    dialog.message = ["Search history cleared."]
    dialog.buttons = ["OK"]

    dialog.observeField("buttonSelected", "OnDialogButtonSelected")

    m.top.getScene().dialog = dialog
end function

function OnDialogButtonSelected()
    m.top.getScene().dialog.close = true
end function

function OnkeyEvent(key as string, press as boolean) as boolean
    settingsScreen = m.top.getScene().findNode("SettingsScreen")
    return settingsScreen@.OnkeyEvent(key, press)
end function
