function SetCaptions(metadata as object, videoPlayer as object, contentNode as object) as void
    if metadata.captions.Count() = 0
        return
    end if
    isoLanguageList = ParseJson(ReadAsciiFile("libpkg:/config/ISO-639-1-to-ISO-639-2T.json"))
    ' TODO: loop through instances until valid captions are found
    ' Captions are rate limited https://github.com/iv-org/invidious/issues/2567
    instance = InvidiousSettings.GetSelectedInstance()

    subtitleTracks = []

    for each caption in metadata.captions
        subtitleTracks.Push({
            Description: caption.label,
            Language: isoLanguageList[caption.language_code] ?? caption.language_code,
            TrackName: instance + caption.url
        })
    end for
    contentNode.ClosedCaptions = True
    contentNode.SubtitleTracks = subtitleTracks
end function
