import "pkg:/source/asyncTask/asyncTask.bs"
import "pkg:/components/Services/SponsorBlock/SponsorBlockTask.bs"
import "pkg:/components/Services/SponsorBlock/SponsorBlockSegmentViewedTask.bs"
import "pkg:/components/Services/SponsorBlock/Notifications/SponsorBlockNotificationUtils.bs"

namespace SponsorBlock

    function SetupSponsorBlock() as void
        if not m.preferences["sponsorblock.enabled"]
            return
        end if

        m.sponsorBlockService = new SponsorBlock.SponsorBlockService()

        ' TODO:P2 get rid of this by using a custom Video UI
        ' The following code serves one purpose: show the segment name of sponsorblock (promo, intro, etc)
        ' 1. Set the chapter label as a child of the trickplay bar
        ' 2. Find the time label (0:00) on the trickplay bar
        ' 3. When the trickplay bar is visible, start a timer
        ' 4. With each tick, parse the time label (if it changed)
        ' 5. If the time label is in a sponsorblock segment, set the chapter label to the segment name
        PositionChapterLabel()
        m.timeLabel = FindTimeLabel()
        m.top.trickPlayBar.observeField("visible", FuncName(OnTrickPlayBarVisible))
        m.chapterLabelTimer = m.top.findNode("chapterLabelTimer")
        m.chapterLabelTimer.observeField("fire", FuncName(OnChapterLabelTimer))

        ' TODO:P1 more precision on when notifications should be visible
        ' Should notifcations be visible when the video is not fullscreen?
        SponsorBlock.SetVisible(m.notifications, m.container.fullscreen)
    end function

    function PositionChapterLabel()
        m.chapterLabel = m.top.findNode("chapterLabel")
        m.chapterLabel.reparent(m.top.trickPlayBar, false)
        trickPlayBarWidth = m.top.trickPlayBar.boundingRect().width
        m.chapterLabel.translation = [trickPlayBarWidth / 4 - m.chapterLabel.width / 2, 25]
    end function

    function FindTimeLabel() as object
        children = m.top.trickPlayBar.getChildren(m.top.trickPlayBar.getChildCount(), 0)
        label = invalid
        for each child in children
            ' It's very unfortunate to have to find a label with 0:00 text,
            ' and that's on the left side of the screen. This might need completely custom trickPlayBar
            if child.isSubtype("Label") and child.text = "0:00"
                if label = invalid
                    label = child
                else
                    if child.translation[0] < label.translation[0]
                        label = child
                    end if
                end if
            end if
        end for
        return label
    end function

    function OnTrickPlayBarVisible()
        if m.top.trickPlayBar.visible
            m.top.chapter = ""
            m.chapterLabelTimer.control = "start"
        else
            m.chapterLabelTimer.control = "stop"
        end if
    end function

    function OnChapterLabelTimer() as void
        if m.timeLabel = invalid or m.skipSegments = invalid
            return
        end if

        if m.sponsorBlockLastTime = m.timeLabel.text
            return
        end if
        m.sponsorBlockLastTime = m.timeLabel.text

        time = TimeUtils.ParseTime(m.timeLabel.text)
        UpdateSponsorBlockChapter(time)
    end function

    function UpdateSponsorBlockChapter(time as integer) as void
        segments = m.skipSegments
        for each segment in segments
            segmentRange = segment["segment"]
            segmentStart = segmentRange[0]
            segmentEnd = segmentRange[1]

            if (segmentStart <= time) and (segmentEnd >= time)
                m.top.chapter = m.sponsorBlockService.SegmentTitle(segment["category"])
                return
            end if
        end for
        m.top.chapter = ""
    end function

    function FetchSponsorBlock(metadata as object) as void
        if not m.preferences["sponsorblock.enabled"]
            return
        end if

        if m.sponsorBlockTask <> invalid
            m.sponsorBlockTask.cancel = true
        end if

        m.sponsorBlockTask = StartAsyncTask(SponsorBlockTask, { metadata: metadata }, OnSponsorBlockTaskResults)
    end function

    function OnSponsorBlockTaskResults(output as object) as void
        m.sponsorBlockTask = invalid
        if output.cancelled
            return
        end if

        if not output.success
            error = ErrorUtils.Format(output.error)
            m.log.error(error)
            return
        end if

        skipSegments = output.result.skipSegments
        if skipSegments = invalid
            return
        end if

        m.skipSegments = skipSegments
        m.top.seekMode = "accurate"

        barPath = output.result.barPath
        if barPath <> invalid
            m.top.trickPlayBar.trackImageUri = barPath
            m.top.trickPlayBar.filledBarBlendColor = "0xFF000080"
        end if

        m.top.UnobserveFieldScoped("position")
        m.top.ObserveFieldScoped("position", FuncName(OnPositionChangeSkipSponsorBlockSections))
    end function

    function OnPositionChangeSkipSponsorBlockSections() as void
        segments = m.skipSegments
        if segments = invalid
            return
        end if

        for each segment in segments
            segmentRange = segment["segment"]
            segmentStart = segmentRange[0]
            segmentEnd = segmentRange[1]
            currentPosition = m.top.position
            if (segmentStart < currentPosition) and ((segmentEnd - 1) > currentPosition)
                action = GetActionForSegment(segment)
                if m.preferences["sponsorblock.show_notifications"] = true
                    SponsorBlock.ShowNotifcation(m.notifications, segment, action)
                end if

                if action = "AutoSkip"
                    m.top.seek = segmentEnd
                    m.log.info("Skipping section:", segment["category"])
                    SendSkippedSectionEvent(segment.UUID)
                    return
                end if
            end if
        end for
    end function

    function GetActionForSegment(segment as object) as dynamic
        category = segment["category"]
        if category = "sponsor"
            return "AutoSkip"
        else if category = "poi_highlight"
            return "ManualSkip"
        else if category = "exclusive_access"
            return "ShowOverlay"
        else if category = "chapter"
            return "ShowOverlay"
        end if
        return ""
    end function

    function SendSkippedSectionEvent(segmentId as string)
        StartAsyncTask(SponsorBlockSegmentViewedTask, { segmentId: segmentId })
    end function

end namespace
