import "DialogPalette.bs"
import "pkg:/source/AsyncTask/AsyncTask.bs"
import "pkg:/source/utils/StringUtils.bs"
import "pkg:/source/utils/Types.bs"
import "pkg:/source/asyncTask/Tasks.bs"

function Init()
    SetPalette(m.top)
    m.urlLabel = m.top.findNode("urlLabel")
    m.qrCode = m.top.findNode("QrCodePoster")

    m.top.width = "920"
    m.top.observeFieldScoped("buttonSelected", FuncName(Close))
end function

function OnNodeReady()
    address = m.webserver@.GetServerAddress()
    isValidAddress = not StringUtils.IsNullOrEmpty(address)
    if isValidAddress
        m.top.url = `${address}/invidious/login`
    else
        m.urlLabel.text = "No IP address found"
    end if

    m.invidious.observeFieldScoped("authToken", FuncName(OnAuthTokenChange))
end function

function Close()
    m.top.close = true
end function

function OnUrlSet()
    url = m.top.url

    m.urlLabel.text = url
    AsyncTask.Start(Tasks.GenerateQrCodeTask, {
        poster: m.qrCode
        text: url
        size: m.qrCode.loadWidth
        padding: 10
    })
end function

function OnAuthTokenChange()
    Close()
end function
