import "pkg:/components/VideoPlayer/Video.bs"

function Init()
    m.buttonGroup = m.top.findNode("NavBarButtonGroup")
    m.top.observeField("focusIndex", "OnFocusIndexChange")
end function

function onKeyEvent(key as string, press as boolean) as boolean
    if not press
        return false
    end if
    if key = "options"
        if ToggleVideoPictureInPicture(m.top)
            return true
        end if
    end if
    if key = "down"
        i = m.top.focusIndex
        target = i + 1
        if target >= m.buttonGroup.getChildCount()
            return false
        end if
        m.top.focusIndex = target
        return true
    else if key = "up"
        i = m.top.focusIndex
        target = i - 1
        if target < 0
            return false
        end if
        m.top.focusIndex = target
        return true
    else if key = "right"
        ' Can only escape to search screen or home screen
        allowedEscapes = { "0": true, "1": true }
        if allowedEscapes[StrI(m.top.focusIndex).Trim()] = true
            m.top.escapeIndex = m.top.focusIndex
            m.top.focusIndex = -1
            return true
        end if
    else if key = "back"
        dialog = ShowDialogButtons("Do you want to exit Playlet?", "Exit", ["Exit", "Cancel"])
        dialog.observeField("buttonSelected", "OnExitDialog", ["buttonSelected"])
        return true
    end if

    return false
end function

function OnExitDialog(event as object)
    buttonIndex = event.GetData()
    if buttonIndex = 0 ' Exit
        ? "EXITING APP"
        m.top.getScene().exitChannel = true
    end if
end function

function OnFocusIndexChange()
    focusIndex = m.top.focusIndex
    childCount = m.buttonGroup.getChildCount()
    for i = 0 to childCount - 1
        if i <> focusIndex
            m.buttonGroup.getChild(i).focus = false
        end if
    end for
    if focusIndex <> -1
        m.buttonGroup.getChild(focusIndex).focus = true
    end if
end function
