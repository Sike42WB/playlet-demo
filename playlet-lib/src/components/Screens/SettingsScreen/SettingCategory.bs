import "pkg:/source/utils/Types.bs"

function Init()
    m.container = m.top.findNode("Container")
    m.displayTextLabel = m.top.findNode("DisplayTextLabel")
    m.descriptionLabel = m.top.findNode("DescriptionLabel")

    m.displayTextLabel.color = m.top.textColor
    m.descriptionLabel.color = m.top.textColor2

    m.top.iconUri = ""
    m.top.focusedIconUri = ""
    m.top.focusFootprintBitmapUri = "pkg:/images/white.9.png"
    m.top.observeField("focusedChild", FuncName(OnFocusedChildChange))
end function

function Resize()
    rect = m.container.boundingRect()
    width = rect.width + m.top.padding * 2
    height = rect.height + m.top.padding * 2

    m.top.minWidth = width < m.top.minimumWidth ? m.top.minimumWidth : width
    m.top.height = height

    m.container.translation = [m.top.padding, m.top.padding]
end function

function OnFocusedChildChange()
    if m.top.hasFocus()
        m.displayTextLabel.color = m.top.focusedTextColor
        m.descriptionLabel.color = m.top.focusedTextColor2
    else
        m.displayTextLabel.color = m.top.textColor
        m.descriptionLabel.color = m.top.textColor2
    end if
end function
