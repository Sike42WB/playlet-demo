import "pkg:/components/Navigation/Navigation.bs"
import "pkg:/source/utils/ArrayUtils.bs"
import "pkg:/source/utils/Locale.bs"
import "pkg:/source/utils/Types.bs"

function Init()
    options = {
        date: [{
            title: Tr(Locale.SearchFilters.AnyDate)
            value: ""
        }, {
            title: Tr(Locale.SearchFilters.LastHour)
            value: "hour"
        }, {
            title: Tr(Locale.SearchFilters.Today)
            value: "today"
        }, {
            title: Tr(Locale.SearchFilters.ThisWeek)
            value: "week"
        }, {
            title: Tr(Locale.SearchFilters.ThisMonth)
            value: "month"
        }, {
            title: Tr(Locale.SearchFilters.ThisYear)
            value: "year"
        }]
        type: [{
            title: Tr(Locale.SearchFilters.AnyType)
            value: ""
        }, {
            title: Tr(Locale.SearchFilters.Video)
            value: "video"
        }, {
            title: Tr(Locale.SearchFilters.Playlist)
            value: "playlist"
        }, {
            title: Tr(Locale.SearchFilters.Channel)
            value: "channel"
        }, {
            title: Tr(Locale.SearchFilters.Movie)
            value: "movie"
        }]
        duration: [{
            title: Tr(Locale.SearchFilters.AnyDuration)
            value: ""
        }, {
            title: Tr(Locale.SearchFilters.Under4Minutes)
            value: "short"
        }, {
            title: Tr(Locale.SearchFilters.FourTo20Minutes)
            value: "medium"
        }, {
            title: Tr(Locale.SearchFilters.Over20Minutes)
            value: "long"
        }]
        features: [{
            title: Tr(Locale.Feed.Live)
            value: "live"
        }, {
            title: Tr(Locale.SearchFilters.FourK)
            value: "4k"
        }, {
            title: Tr(Locale.SearchFilters.HD)
            value: "hd"
        }, {
            title: Tr(Locale.SearchFilters.SubtitlesCC)
            value: "subtitles"
        }, {
            title: Tr(Locale.SearchFilters.CreativeCommons)
            value: "creative_commons"
        }, {
            title: Tr(Locale.SearchFilters.ThreeSixty)
            value: "360"
        }, {
            title: Tr(Locale.SearchFilters.VR180)
            value: "vr180"
        }, {
            title: Tr(Locale.SearchFilters.ThreeD)
            value: "3d"
        }, {
            title: Tr(Locale.SearchFilters.HDR)
            value: "hdr"
        }, {
            title: Tr(Locale.SearchFilters.Purchased)
            value: "purchased"
        }, {
            title: Tr(Locale.SearchFilters.Location)
            value: "location"
        }]
        sort: [{
            title: Tr(Locale.SearchFilters.Relevance)
            value: ""
        }, {
            title: Tr(Locale.SearchFilters.UploadDate)
            value: "date"
        }, {
            title: Tr(Locale.SearchFilters.ViewCount)
            value: "views"
        }, {
            title: Tr(Locale.SearchFilters.Rating)
            value: "rating"
        }]
    }

    m.controls = {
        date: m.top.findNode("UploadDateList")
        type: m.top.findNode("TypeList")
        duration: m.top.findNode("DurationList")
        features: m.top.findNode("FeaturesList")
        sort: m.top.findNode("SortList")
    }

    for each filterKey in m.controls
        control = m.controls[filterKey]
        control.addFields({ filterKey: filterKey })
    end for

    m.contentNodes = {
        date: m.controls.date.content
        type: m.controls.type.content
        duration: m.controls.duration.content
        features: m.controls.features.content
        sort: m.controls.sort.content
    }

    PopulateLists(m.contentNodes, options)

    navigationNodes = [m.controls.date, m.controls.type, m.controls.duration, m.controls.features, m.controls.sort]
    for i = 0 to navigationNodes.count() - 2
        SetNavigation(navigationNodes[i], "right", navigationNodes[i + 1])
    end for
    for i = 1 to navigationNodes.count() - 1
        SetNavigation(navigationNodes[i], "left", navigationNodes[i - 1])
    end for
end function

function PopulateLists(contentNodes as object, options as object)
    for each key in options
        contentNode = contentNodes[key]
        list = options[key]
        for each item in list
            node = CreateObject("roSGNode", "ContentNode")
            node.title = item.title
            node.addFields({ value: item.value })
            contentNode.appendChild(node)
        end for
    end for
end function

function OnNodeReady()
    RefreshUI()

    m.controls.date.ObserveField("checkedItem", FuncName(OnListChecked))
    m.controls.type.ObserveField("checkedItem", FuncName(OnListChecked))
    m.controls.duration.ObserveField("checkedItem", FuncName(OnListChecked))
    m.controls.features.ObserveField("checkedState", FuncName(OnListChecked))
    m.controls.sort.ObserveField("checkedItem", FuncName(OnListChecked))
end function

function RefreshUI()
    filters = m.searchScreen.filters
    for each key in filters
        control = m.controls[key]
        value = filters[key]
        if IsString(value)
            SetCheckedItem(control, value)
        else if IsArray(value)
            SetCheckedItemArray(control, value)
        end if
    end for
end function

function SetCheckedItem(control as object, value as string) as void
    content = control.content
    childCount = content.getChildCount()
    for i = 0 to childCount - 1
        child = content.getChild(i)
        if child.value = value
            control.checkedItem = i
            return
        end if
    end for
end function

function SetCheckedItemArray(control as object, values as object) as void
    content = control.content
    childCount = content.getChildCount()
    checkedState = []
    for i = 0 to childCount - 1
        child = content.getChild(i)
        value = child.value
        checkedState.push(ArrayUtils.IndexOfString(values, value) >= 0)
    end for
    control.checkedState = checkedState
end function

function OnFocusChange() as void
    if not m.top.focus
        return
    end if

    NodeSetFocus(m.controls.date, true)
end function

function OnListChecked(event as object)
    control = event.getRoSGNode()
    filterKey = control.filterKey

    state = m.searchScreen.filters
    if IsString(state[filterKey])
        SetFilterString(control)
    else if IsArray(state[filterKey])
        SetFilterArray(control)
    end if
end function

function SetFilterString(control as object) as void
    content = control.content
    selectedNode = content.getChild(control.checkedItem)
    value = selectedNode.value

    filterKey = control.filterKey
    state = m.searchScreen.filters
    state[filterKey] = value
    m.searchScreen.filters = state
end function

function SetFilterArray(control as object) as void
    content = control.content
    checkedState = control.checkedState

    values = []
    for i = 0 to checkedState.count() - 1
        if checkedState[i]
            node = content.getChild(i)
            values.push(node.value)
        end if
    end for

    filterKey = control.filterKey
    state = m.searchScreen.filters
    state[filterKey] = values
    m.searchScreen.filters = state
end function

function OnkeyEvent(key as string, press as boolean) as boolean
    navigation = NavigationKeyHandler(key, press)
    if navigation.handled
        m.currentNavigtionNode = navigation.node
        return true
    end if

    if key = "options" or key = "play" or key = "pause" or key = "playonly"
        ' A pass-through to the app controller, so it can toggle picture-in-picture and pause/play
        return false
    end if

    if key = "back" and press
        Close()
        return true
    end if

    return true
end function

function Close()
    m.appController@.PopScreen()
end function
