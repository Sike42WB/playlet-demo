import "pkg:/components/Dialog/DialogUtils.bs"
import "pkg:/components/Navigation/Navigation.bs"
import "pkg:/source/AsyncTask/AsyncTask.bs"
import "pkg:/source/AsyncTask/AsyncTask.bs"
import "pkg:/source/AsyncTask/Tasks.bs"
import "pkg:/source/AsyncTask/Tasks.bs"
import "pkg:/source/utils/ErrorUtils.bs"
import "pkg:/source/utils/FocusManagement.bs"
import "pkg:/source/utils/Logging.bs"
import "pkg:/source/utils/StringUtils.bs"

function Init()
    m.linkCodeLabel = m.top.findNode("LinkCodeLabel")
    m.linkWithTvButton = m.top.findNode("LinkWithTvButton")
    m.webAppButton = m.top.findNode("WebAppButton")
    m.linkWithTvPanel = m.top.findNode("LinkWithTvPanel")
    m.webAppPanel = m.top.findNode("WebAppPanel")
end function

function OnNodeReady()
    SetNavigation(m.webAppButton, "left", m.navBar)
    SetNavigation(m.webAppButton, "right", m.linkWithTvButton)
    SetNavigation(m.linkWithTvButton, "left", m.webAppButton)

    SetNavigation(invalid, "back", m.navBar)
    SetNavigation(invalid, "left", m.navBar)

    address = m.webserver@.GetServerAddress()
    isValidAddress = not StringUtils.IsNullOrEmpty(address)
    if isValidAddress
        m.top.url = address
    end if
    m.top.findNode("noInternet").visible = not isValidAddress
    m.top.findNode("yesInternet").visible = isValidAddress
    m.top.focusable = isValidAddress

    m.top.observeField("visible", FuncName(OnVisibleChanged))

    m.linkWithTvPanel.observeField("visible", FuncName(OnLinkWithTvPanelVisibleChanged))
    m.linkWithTvPanel.visible = false
end function

function OnFocusChange() as void
    if not m.top.focus
        return
    end if
    NodeSetFocus(m.webAppButton, true)
    m.webAppPanel.visible = true
    m.linkWithTvPanel.visible = false
end function

function OnUrlSet()
    url = m.top.url

    urlLabel = m.top.findNode("urlLabel")
    urlLabel.text = url

    qrCodePoster = m.top.findNode("QrCodePoster")
    AsyncTask.Start(Tasks.GenerateQrCodeTask, {
        poster: qrCodePoster
        text: url
        size: qrCodePoster.loadWidth
        padding: 20
    })
end function

function OnkeyEvent(key as string, press as boolean) as boolean
    navigation = NavigationKeyHandler(key, press)
    if navigation.handled
        if navigation.node.isSameNode(m.linkWithTvButton)
            m.linkWithTvPanel.visible = true
            m.webAppPanel.visible = false
        else if navigation.node.isSameNode(m.webAppButton)
            m.linkWithTvPanel.visible = false
            m.webAppPanel.visible = true
        end if
        return true
    end if
    return false
end function

function OnVisibleChanged()
    if not m.top.visible
        m.linkWithTvPanel.visible = false
        m.webAppPanel.visible = true
    end if
end function

function OnLinkWithTvPanelVisibleChanged()
    if m.linkWithTvPanel.visible
        GeneratePairingCode()
    else
        if m.linkCodeTask <> invalid
            m.linkCodeTask.cancel = true
            m.linkCodeTask = invalid
        end if
        m.linkCodeLabel.text = ""
    end if
end function

function GeneratePairingCode()
    if m.linkCodeTask <> invalid
        m.linkCodeTask.cancel = true
    end if

    m.linkCodeTask = AsyncTask.Start(Tasks.LoungeGenerateManualPairingCodeTask, {
        loungeToken: m.loungeService.loungeToken
        screenId: m.loungeService.screenId
        ' Use deviceName as screenName
        screenName: m.loungeService.deviceName
        deviceId: m.loungeService.deviceId
    }, OnLoungeGenerateManualPairingCodeTaskResult)
end function

function OnLoungeGenerateManualPairingCodeTaskResult(output as object) as void
    m.linkCodeTask = invalid

    if output.cancelled
        return
    end if

    if not output.success or not output.result.success
        ' output.error for unhandled exception
        error = output.error
        if error = invalid
            ' output.result.error for network errors
            error = output.result.error
        end if
        error = ErrorUtils.Format(error)
        LogError(error)

        message = `Failed to generate manual pairing code\n${error}`
        DialogUtils.ShowDialog(message, "Get TV code fail", true)
        return
    end if

    manualLinkCode = output.result.code
    if StringUtils.IsNullOrEmpty(manualLinkCode)
        m.linkCodeLabel.text = ""
        return
    end if

    ' insert a space every 3 characters.
    parts = []
    for i = 0 to manualLinkCode.Len() - 1 step 3
        parts.Push(manualLinkCode.Mid(i, 3))
    end for
    linkCode = parts.Join(" ")

    m.linkCodeLabel.text = linkCode
end function
