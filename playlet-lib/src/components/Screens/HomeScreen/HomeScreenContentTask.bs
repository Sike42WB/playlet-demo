import "pkg:/components/Screens/HomeScreen/FeedLoadState.bs"

@asynctask
function HomeScreenContentTask(input as object)
    contentFilePath = input.contentFilePath
    rowList = input.node

    contentNode = CreateObject("roSGNode", "ContentNode")
    contentNode.AddField("isLoaded", "boolean", false)
    contentNode.isLoaded = false

    feeds = ParseJson(ReadAsciiFile(contentFilePath))

    for each feed in feeds
        feedContentNode = CreateObject("roSGNode", "ContentNode")
        feedContentNode.AddField("feed", "assocarray", false)
        feedContentNode.AddField("loadState", "string", false)

        feedContentNode.feed = feed
        feedContentNode.loadState = FeedLoadState.None

        feedContentNode.title = feed.title

        for i = 1 to 3
            placeHolder = feedContentNode.createChild("ContentNode")
            placeHolder.AddField("isPlaceHolder", "boolean", false)
            placeHolder.isPlaceHolder = true
        end for

        contentNode.appendChild(feedContentNode)
    end for

    rowList.content = contentNode
end function
