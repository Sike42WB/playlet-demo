import "pkg:/components/ContextMenu/ContextMenuUtils.bs"
import "pkg:/components/Navigation/Navigation.bs"
import "pkg:/source/utils/FocusManagement.bs"
import "pkg:/source/utils/Types.bs"

function Init()
    m.noBookmarks = m.top.findNode("noBookmarks")
    m.yesBookmarks = m.top.findNode("yesBookmarks")
    m.rowList = m.top.FindNode("rowList")
    m.rowList.screen = m.top
    m.isDirty = true

    m.top.ObserveField("visible", FuncName(OnVisibleChange))
end function

function OnNodeReady()
    SetNavigation(invalid, "back", m.navBar)
    SetNavigation(invalid, "left", m.navBar)

    m.rowList@.BindNode(invalid)

    OnBookmarksChange()
    m.bookmarks.ObserveField("contentChange", FuncName(OnBookmarksChange))
end function

function OnFocusChange() as void
    if not m.top.focus
        return
    end if

    if m.yesBookmarks.visible
        NodeSetFocus(m.rowList, true)
    else
        NodeSetFocus(m.navBar, true)
    end if
end function

function OnVisibleChange()
    if m.top.visible and m.isDirty
        OnBookmarksChange()
    end if
end function

function Refresh(unused as dynamic)
    m.isDirty = true
    OnBookmarksChange()
end function

function OnBookmarksChange() as void
    if not m.top.visible
        m.isDirty = true
        return
    end if
    m.isDirty = false

    hasBookmarks = m.bookmarks.content.getChildCount() > 0
    m.noBookmarks.visible = not hasBookmarks
    m.yesBookmarks.visible = hasBookmarks
    m.top.focusable = hasBookmarks

    if hasBookmarks
        SetRowListContent(m.bookmarks.content)
    else
        if m.rowList.hasFocus()
            NodeSetFocus(m.navBar, true)
        end if
    end if
end function

function SetRowListContent(bookmarksContent as object)
    bookmarks = bookmarksContent.getChildren(-1, 0)

    contentData = []
    for each bookmarkGroup in bookmarks
        items = bookmarkGroup.getChildren(-1, 0)
        rowData = {
            title: bookmarkGroup.title,
            items: []
        }

        for each item in items
            if item.type = "video"
                rowItemData = {
                    apiType: "Invidious",
                    endpoint: "video_info",
                    pathParams: {
                        id: item.itemId
                    }
                }
                rowData.items.push(rowItemData)
            else if item.type = "playlist"
                rowItemData = {
                    apiType: "Invidious",
                    endpoint: "playlist_info",
                    pathParams: {
                        plid: item.itemId
                    }
                }
                rowData.items.push(rowItemData)
                if items.Count() = 1
                    rowItemData = {
                        apiType: "Invidious",
                        endpoint: "playlist",
                        pathParams: {
                            plid: item.itemId
                        }
                    }
                    rowData.items.push(rowItemData)
                end if
            else if item.type = "channel"
                rowItemData = {
                    apiType: "Invidious",
                    endpoint: "channel_info",
                    pathParams: {
                        ucid: item.itemId
                    }
                }
                rowData.items.push(rowItemData)
                if items.Count() = 1
                    rowItemData = {
                        apiType: "Invidious",
                        endpoint: "channel_videos",
                        pathParams: {
                            ucid: item.itemId
                        }
                    }
                    rowData.items.push(rowItemData)
                end if
            else if item.type = "feed"
                rowData.items.append(item.feed.items)
            end if
        end for

        contentData.push(rowData)
    end for

    m.rowList.contentData = contentData
end function

function OnkeyEvent(key as string, press as boolean) as boolean
    if NavigationKeyHandler(key, press).handled
        return true
    end if
    return false
end function

function GetContextMenuOptionsForItem(rowItem as object) as object
    refreshItem = ContextMenuUtils.CreateOption("Refresh Bookmarks", m.top, "Refresh", [invalid])
    return [refreshItem]
end function
