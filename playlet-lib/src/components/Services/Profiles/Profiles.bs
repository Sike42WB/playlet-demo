import "pkg:/source/AsyncTask/AsyncTask.bs"
import "pkg:/source/AsyncTask/Tasks.bs"
import "pkg:/source/utils/ArrayUtils.bs"
import "pkg:/source/utils/ColorUtils.bs"
import "pkg:/source/utils/CryptoUtils.bs"
import "pkg:/source/utils/Logging.bs"
import "pkg:/source/utils/RegistryUtils.bs"
import "pkg:/source/utils/StringUtils.bs"

' TODO:P1 add the ability to change color of profiles
function Init()
    m.disableSavingToRegistry = false
    content = m.top.findNode("ProfilesContent")
    m.top.content = content
    m.profilesContent = content.getChild(0)
end function

function OnNodeReady()
    LogInfo("Profiles node is ready")
    LoadProfilesFromRegistry()
end function

function LoadProfilesFromRegistry()
    LogInfo("LoadProfilesFromRegistry")

    ' Step 1: Load user profiles from registry
    userProfiles = RegistryUtils.Read(RegistryUtils.USER_PROFILES)

    if userProfiles = invalid
        userProfiles = {
            profiles: []
            currentProfile: invalid
        }
    else
        userProfiles = ParseJson(userProfiles)
    end if

    ' Step 2: Migrate existing profiles to new format (if needed)
    isDirty = MigrateExistingProfiles(userProfiles)

    ' Step 3: Save user preferences (if needed)
    if isDirty
        SaveUserProfilesToRegistry()
    end if

    ' Step 4: Load user profiles into content nodes
    profileNodes = []
    for each profile in userProfiles.profiles
        profileNode = CreateObject("roSGNode", "ProfileContentNode")
        profileNode.id = profile.id
        profileNode.type = profile.type
        profileNode.username = profile.username
        profileNode.serverUrl = profile.serverUrl
        profileNode.accessToken = profile.accessToken
        profileNode.color = profile.color

        profileNodes.Push(profileNode)
    end for

    m.profilesContent.insertChildren(profileNodes, 0)

    if not StringUtils.IsNullOrEmpty(userProfiles.currentProfile)
        for each profileNode in profileNodes
            if profileNode.id = userProfiles.currentProfile
                ' Setting current profile will also save the profiles to registry
                ' but we just saved the profiles
                m.disableSavingToRegistry = true
                m.top.currentProfile = profileNode
                m.disableSavingToRegistry = false
                exit for
            end if
        end for
    end if

    if profileNodes.Count() > 0
        AsyncTask.Start(Tasks.ProfilesVerifyTokensTask, {
            profiles: m.top
            invidious: m.invidious
            profileNodes: profileNodes
        })
    end if
end function

function MigrateExistingProfiles(userProfiles as object) as boolean
    tokenPayload = RegistryUtils.Read(RegistryUtils.INVIDIOUS_TOKEN)
    if tokenPayload = invalid
        return false
    end if

    authToken = ParseJson(tokenPayload)
    RegistryUtils.Delete(RegistryUtils.INVIDIOUS_TOKEN)

    if authToken = invalid or authToken.instance = invalid or authToken.token = invalid or authToken.username = invalid
        return false
    end if

    id = CreateProfileId(authToken.username, authToken.instance)
    for each profile in userProfiles.profiles
        if profile.id = id
            return false
        end if
    end for

    profile = {
        "id": id
        "type": "invidious"
        "username": authToken.username
        "serverUrl": authToken.instance
        "accessToken": authToken.token
        "color": ColorUtils.RandomSoftColorHex()
    }

    userProfiles.profiles.Unshift(profile)
    if StringUtils.IsNullOrEmpty(userProfiles["currentProfile"])
        userProfiles["currentProfile"] = profile.id
    end if

    return true
end function

function SaveUserProfilesToRegistry() as void
    if m.disableSavingToRegistry
        return
    end if

    userProfiles = GetProfilesDTO(true)

    userProfiles.__version = m.top.__version
    RegistryUtils.Write(RegistryUtils.USER_PROFILES, FormatJson(userProfiles))
end function

function GetProfilesDTO(includeAccessToken as boolean) as object
    userProfiles = {
        "profiles": []
        "currentProfile": invalid
    }

    profileNodes = m.profilesContent.getChildren(-1, 0)

    for each profileNode in profileNodes
        if profileNode.type = "login"
            continue for
        end if

        color = profileNode.color
        if IsInt(color)
            color = ColorUtils.IntToHex(color)
        end if

        profile = {
            "id": profileNode.id
            "type": profileNode.type
            "username": profileNode.username
            "serverUrl": profileNode.serverUrl
            "color": color
        }

        if includeAccessToken
            profile["accessToken"] = profileNode.accessToken
        end if

        userProfiles.profiles.Push(profile)
    end for

    currentProfile = m.top.currentProfile
    if currentProfile <> invalid
        userProfiles["currentProfile"] = currentProfile.id
    end if

    return userProfiles
end function

function OnLoginWithProfile(event as object)
    newProfile = event.getData()
    isNewProfile = true
    if StringUtils.IsNullOrEmpty(newProfile.id)
        newProfile.id = CreateProfileId(newProfile.username, newProfile.serverUrl)
    end if

    existingProfile = invalid
    profiles = m.profilesContent.getChildren(-1, 0)
    for each profile in profiles
        if profile.id = newProfile.id
            existingProfile = profile
            isNewProfile = false
            exit for
        end if
    end for

    if not isNewProfile
        ' Refresh token of existing profile
        existingProfile.accessToken = newProfile.accessToken
        newProfile = existingProfile
    end if

    if isNewProfile
        newProfile.color = ColorUtils.RandomSoftColorHex()
        m.profilesContent.insertChild(newProfile, 0)
    end if

    m.top.currentProfile = newProfile
end function

function OnLogoutWithProfile(event as object) as void
    logoutWithProfile = event.getData()
    if logoutWithProfile = invalid
        return
    end if

    profile = m.profilesContent.findNode(logoutWithProfile.id)
    if profile = invalid
        return
    end if

    m.profilesContent.removeChild(profile)
    currentProfile = m.top.currentProfile
    if currentProfile <> invalid and currentProfile.id = profile.id
        m.top.currentProfile = invalid
    else
        OnCurrentProfile()
    end if

    AsyncTask.Start(Tasks.ProfilesInvidiousUnregisterTokenTask, {
        profiles: m.top
        invidious: m.invidious
        profileNodes: [profile]
    })
end function

function OnLogoutWithProfiles(event as object) as void
    logoutWithProfiles = event.getData()
    if logoutWithProfiles = invalid or logoutWithProfiles.Count() = 0
        return
    end if

    currentProfile = m.top.currentProfile
    logoutFromCurrentProfile = false
    for each profile in logoutWithProfiles
        profileNode = m.profilesContent.findNode(profile.id)
        if profileNode = invalid
            continue for
        end if

        m.profilesContent.removeChild(profileNode)
        if currentProfile <> invalid and currentProfile.id = profile.id
            logoutFromCurrentProfile = true
        end if
    end for

    if logoutFromCurrentProfile
        m.top.currentProfile = invalid
    else
        OnCurrentProfile()
    end if

    AsyncTask.Start(Tasks.ProfilesInvidiousUnregisterTokenTask, {
        profiles: m.top
        invidious: m.invidious
        profileNodes: logoutWithProfiles
    })
end function

function OnLogoutWithAccessToken(event as object) as void
    logoutWithAccessToken = event.getData()
    if StringUtils.IsNullOrEmpty(logoutWithAccessToken)
        return
    end if

    ' We are not going to invalidate the token on the server
    ' because we are likely dealing with an invalid token
    ' in the first place. All we need to do is remove the
    ' profile from the list of profiles.

    allProfiles = m.profilesContent.getChildren(-1, 0)
    for each profile in allProfiles
        if profile.accessToken = logoutWithAccessToken
            m.profilesContent.removeChild(profile)
            currentProfile = m.top.currentProfile
            if currentProfile <> invalid and currentProfile.id = profile.id
                m.top.currentProfile = invalid
            else
                OnCurrentProfile()
            end if
        end if
    end for
end function

function OnCurrentProfile() as void
    LogInfo("OnCurrentProfile")

    currentProfile = m.top.currentProfile
    allProfiles = m.profilesContent.getChildren(-1, 0)
    if currentProfile = invalid
        for each profile in allProfiles
            profile.isSelected = false
        end for
        m.invidious.authToken = invalid
        LogInfo("Current profile set to <invalid>")
        SaveUserProfilesToRegistry()
        return
    end if

    for each profile in allProfiles
        profile.isSelected = profile.id = currentProfile.id
    end for

    m.invidious.authToken = {
        token: currentProfile.accessToken
        instance: currentProfile.serverUrl
        username: currentProfile.username
    }
    LogInfo("Current profile set to", currentProfile.username, "@", currentProfile.serverUrl)
    SaveUserProfilesToRegistry()
end function

function CreateProfileId(username as string, serverUrl as string) as string
    return "v1:" + CryptoUtils.GetMd5(username + serverUrl)
end function
