import "pkg:/components/Services/LoungeService/LoungeConstants.bs"
import "pkg:/source/utils/StringUtils.bs"
import "pkg:/source/utils/Types.bs"

namespace LoungeMessages

    function OnHasPreviousNextChanged() as object
        message = {
            commandType: COMMAND_ON_STATE_CHANGE
        }
        commandArgs = {}
        commandArgs[FIELD_HAS_PREVIOUS] = false
        commandArgs[FIELD_HAS_NEXT] = false
        message.commandArgs = commandArgs
        return message
    end function

    function OnStateChange(player as object, overrides = invalid as object) as object
        message = {
            commandType: COMMAND_ON_STATE_CHANGE
        }
        if player = invalid
            return message
        end if

        commandArgs = {}

        commandArgs[FIELD_STATE] = GetPlayerState(player)
        commandArgs[FIELD_CURRENT_TIME] = ValidDouble(player.position)
        commandArgs[FIELD_SEEKABLE_START_TIME] = 0

        content = player.content
        if content <> invalid
            metadata = content.metadata
            if metadata <> invalid
                lengthSeconds = metadata.lengthSeconds
                if IsInt(lengthSeconds)
                    commandArgs[FIELD_DURATION] = lengthSeconds
                    commandArgs[FIELD_SEEKABLE_END_TIME] = lengthSeconds

                    if commandArgs[FIELD_STATE] = PlayerStatus.PLAYING or commandArgs[FIELD_STATE] = PlayerStatus.PAUSED or commandArgs[FIELD_STATE] = PlayerStatus.LOADING
                        commandArgs[FIELD_LOADED_TIME] = lengthSeconds
                    end if
                end if
            end if
        end if

        if overrides <> invalid
            commandArgs.Append(overrides)
        end if

        message.commandArgs = commandArgs

        return message
    end function

    function NowPlaying(player as object, overrides = invalid as object) as object
        message = {
            commandType: COMMAND_NOW_PLAYING
        }

        if player = invalid
            return message
        end if

        commandArgs = {}

        commandArgs[FIELD_CURRENT_TIME] = ValidDouble(player.position)
        commandArgs[FIELD_SEEKABLE_START_TIME] = 0
        commandArgs[FIELD_CURRENT_INDEX] = 0

        commandArgs[FIELD_STATE] = GetPlayerState(player)

        content = player.content
        if content <> invalid
            metadata = content.metadata
            if metadata <> invalid
                lengthSeconds = metadata.lengthSeconds
                if IsInt(lengthSeconds)
                    commandArgs[FIELD_DURATION] = lengthSeconds
                    commandArgs[FIELD_SEEKABLE_END_TIME] = lengthSeconds

                    if commandArgs[FIELD_STATE] = PlayerStatus.PLAYING or commandArgs[FIELD_STATE] = PlayerStatus.PAUSED or commandArgs[FIELD_STATE] = PlayerStatus.LOADING
                        commandArgs[FIELD_LOADED_TIME] = lengthSeconds
                    end if
                end if
                videoId = metadata.videoId
                if not StringUtils.IsNullOrEmpty(videoId)
                    commandArgs[FIELD_VIDEO_ID] = videoId
                end if
            end if
        end if

        if overrides <> invalid
            commandArgs.Append(overrides)
        end if

        message.commandArgs = commandArgs
        return message
    end function

    function GetPlayerState(player as object) as PlayerStatus
        if player = invalid
            return PlayerStatus.IDLE
        end if

        state = player.state
        if state = "playing"
            return PlayerStatus.PLAYING
        else if state = "paused"
            return PlayerStatus.PAUSED
        else if state = "buffering"
            return PlayerStatus.LOADING
        else if state = "stopped" or state = "stopping" or state = "finished"
            return PlayerStatus.STOPPED
        end if

        return PlayerStatus.IDLE
    end function

end namespace
