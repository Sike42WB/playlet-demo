import "pkg:/components/parts/AutoBind/OnNodeReadyNoOp.bs"
import "pkg:/components/Services/LoungeService/LoungeApi.bs"

function Init()
    m.top.functionName = "LoungeServiceLoop"

    deviceInfo = CreateObject("roDeviceInfo")
    m.top.deviceId = deviceInfo.GetChannelClientId()
    deviceFriendlyName = deviceInfo.GetFriendlyName()
    if StringUtils.IsNullOrEmpty(deviceFriendlyName)
        deviceFriendlyName = "TV"
    end if
    m.top.deviceName = "Playlet on " + deviceFriendlyName
end function

function StartService(_unused as dynamic) as boolean
    if m.top.isRunning = true
        return true
    end if

    m.top.control = "run"
    return true
end function

function StopService(_unused as dynamic) as void
    if m.top.isRunning = false
        return
    end if

    m.top.shouldQuit = true
end function

function LoungeServiceLoop()
    messagePort = CreateObject("roMessagePort")
    api = new LoungeApi(m.top, m.top.playQueue)

    m.top.screenId = api.GetScreenId()
    m.top.loungeToken = api.GetToken()

    m.top.observeFieldScoped("outgoingMessages", messagePort)

    api.GetSessionData()
    api.FetchCommandsRpc(messagePort)
    isOpen = false

    while true
        if m.top.shouldQuit = true
            exit while
        end if

        msg = wait(60000, messagePort)
        msgType = type(msg)

        if msgType = "roSGNodeEvent"
            field = msg.GetField()
            if field = "outgoingMessages"
                messages = msg.GetData()
                api.SendMessages(messages)
            end if
        end if

        isOpen = api.Poll(msg)

        if not isOpen
            sleep(3000)
            api.FetchCommandsRpc(messagePort)
        end if
    end while
end function
