import "pkg:/components/Services/LoungeService/LoungeApi.bs"
import "pkg:/source/services/HttpClient.bs"
import "pkg:/source/utils/StringUtils.bs"

@asynctask
function LoungeGenerateManualPairingCodeTask(input as object) as object
    loungeService = input.loungeService

    if StringUtils.IsNullOrEmpty(input.loungeToken)
        messagePort = CreateObject("roMessagePort")
        loungeService.observeFieldScopedEx("loungeToken", messagePort)
        loungeService.join = true
        message = wait(30000, messagePort)
        loungeService.unobserveFieldScoped("loungeToken")
        if message = invalid or StringUtils.IsNullOrEmpty(message.GetData())
            return {
                "success": false
                "error": "Couldn't join lounge"
            }
        end if

        input.screenId = loungeService.screenId
        input.loungeToken = loungeService.loungeToken
    end if

    form = {
        "lounge_token": input.loungeToken
        "screen_id": input.screenId
        "screen_name": input.screenName
        "device_id": input.deviceId
    }

    for each key in form
        if StringUtils.IsNullOrEmpty(form[key])
            return {
                "success": false
                "error": key + " is empty"
            }
        end if
    end for

    form["access_type"] = "permanent"
    form["app"] = "Playlet"

    request = HttpClient.Post("https://www.youtube.com/api/lounge/pairing/get_pairing_code?ctx=pair", "")
    request.UrlEncodedForm(form)

    response = request.Await()
    if not response.IsSuccess()
        return {
            "success": false
            "error": response.ErrorMessage()
        }
    end if

    code = response.Text()
    return {
        "success": true
        "code": code
    }
end function
