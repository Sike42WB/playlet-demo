namespace ContinueWatchingUtils

    const MAX_VIDEOS = 100

    function GetOrCreateNodeForVideo(continueWatchingNode as object, videoId as string, maxVideos = 100 as integer) as object
        content = continueWatchingNode.content
        node = content.findNode(videoId)
        if node <> invalid
            return node
        end if

        node = CreateObject("roSGNode", "VideoProgressContentNode")
        node.id = videoId
        node.videoId = videoId
        content.insertChild(node, 0)

        nodeCount = content.getChildCount()
        if nodeCount > maxVideos
            excess = nodeCount - maxVideos
            content.removeChildrenIndex(excess, maxVideos)
        end if

        return node
    end function

    function MarkWatchDate(progressNode as object)
        date = CreateObject("roDateTime")
        progressNode.date = date.asSeconds()
        parent = progressNode.getParent()
        if parent <> invalid
            parent.removeChild(progressNode)
            parent.insertChild(progressNode, 0)
        end if
    end function

    function RemoveNodeForVideo(continueWatchingNode as object, videoId as string)
        content = continueWatchingNode.content
        node = content.findNode(videoId)
        if node <> invalid
            content.removeChild(node)
        end if
    end function

    function Clear(continueWatchingNode as object)
        content = continueWatchingNode.content
        nodeCount = content.getChildCount()
        if nodeCount > 0
            content.removeChildrenIndex(nodeCount, 0)
        end if

        continueWatchingNode.save = true
    end function
end namespace
