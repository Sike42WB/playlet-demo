import "pkg:/components/Services/ContinueWatching/ContinueWatchingUtils.bs"
import "pkg:/components/Services/Invidious/InvidiousService.bs"
import "pkg:/source/utils/MathUtils.bs"

namespace Http

    const CONTINUE_WATCHING_PAGE_SIZE = 5

    class ContinueWatchingRouter extends HttpRouter

        function new()
            super()

            m.Get("/api/continue-watching", function(context as object) as boolean
                request = context.request
                response = context.response
                router = context.router
                task = context.server.task
                invidiousNode = task.invidious
                preferencesNode = task.preferences
                continueWatchingNode = task.continueWatching

                isEnabled = preferencesNode["continue_watching.enabled"] = true
                if not isEnabled
                    response.Default(403, "Continue watching is disabled from the settings.")
                    return true
                end if

                page = 1
                if not StringUtils.IsNullOrEmpty(request.query.page)
                    page = request.query.page.ToInt()
                    page = MathUtils.Max(page, 1)
                end if

                totalCount = continueWatchingNode.content.getChildCount()
                startIndex = (page - 1) * CONTINUE_WATCHING_PAGE_SIZE
                if startIndex >= totalCount
                    response.Json([])
                    return true
                end if

                videoCount = MathUtils.Min(CONTINUE_WATCHING_PAGE_SIZE, totalCount - startIndex)
                nodes = continueWatchingNode.content.getChildren(videoCount, startIndex)

                videoIds = []
                for each node in nodes
                    videoIds.push(node.videoId)
                end for

                service = new Invidious.InvidiousService(invidiousNode)
                instance = service.GetInstance()

                videoRequests = []
                for each videoId in videoIds
                    videoRequests.push(router.GetVideoMetadata(instance, videoId))
                end for

                videoResponses = []
                for each videoRequest in videoRequests
                    videoResponses.push(videoRequest.Await())
                end for

                videos = []
                for each videoResponse in videoResponses
                    if videoResponse.StatusCode() = 200
                        videoInfo = videoResponse.Json()
                        videoInfo.type = "video"
                        ' Remove unnecessary fields, keep payload small
                        videoInfo.Delete("adaptiveFormats")
                        videoInfo.Delete("formatStreams")
                        videoInfo.Delete("storyboards")
                        videoInfo.Delete("recommendedVideos")
                        videos.push(videoInfo)
                    end if
                end for

                response.Json(videos)
                return true
            end function)

            m.Delete("/api/continue-watching", function(context as object) as boolean
                response = context.response
                task = context.server.task
                continueWatchingNode = task.continueWatching

                ContinueWatchingUtils.Clear(continueWatchingNode)

                response.Default(204, "OK")
                return true
            end function)
        end function

        function GetVideoMetadata(instance as string, videoId as string, cancellation = invalid as object) as object
            ' _playlet_ref=video_info so that we give it it's own cache that lasts longer
            url = `${instance}${Invidious.VIDEOS_ENDPOINT}/${videoId}?_playlet_ref=video_info`

            request = HttpClient.Get(url)
            ' 3 days of cache
            request.CacheSeconds(60 * 60 * 24 * 3)
            request.Cancellation(cancellation)

            ' Send, and not await, since we are launching many requests at once
            return request.Send()
        end function

    end class

end namespace
