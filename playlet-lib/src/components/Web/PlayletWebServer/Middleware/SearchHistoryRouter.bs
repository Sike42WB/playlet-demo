import "pkg:/components/Services/SearchHistory/SearchHistoryUtils.bs"

namespace Http

    class SearchHistoryRouter extends HttpRouter

        function new()
            super()

            m.Get("/api/search-history", function(context as object) as boolean
                response = context.response
                task = context.server.task
                preferences = task.preferences

                response.Json(SearchHistoryUtils.GetAllSaved(preferences))
                return true
            end function)

            m.Put("/api/search-history", function(context as object) as boolean
                request = context.request
                response = context.response
                task = context.server.task
                preferences = task.preferences

                payload = request.Json()
                if payload = invalid
                    response.Default(400, `Invalid JSON payload`)
                    return true
                end if

                query = payload.query
                if query = invalid
                    response.Default(400, `missing "query" property in JSON payload`)
                    return true
                end if

                SearchHistoryUtils.Save(preferences, query)
                response.Json(SearchHistoryUtils.GetAllSaved(preferences))
                return true
            end function)

            m.Delete("/api/search-history", function(context as object) as boolean
                response = context.response

                SearchHistoryUtils.Clear()
                response.Default(204, "OK")
                return true
            end function)

        end function

    end class

end namespace
