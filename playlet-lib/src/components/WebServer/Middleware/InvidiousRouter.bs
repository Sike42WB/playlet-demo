import "pkg:/source/services/InvidiousSettings.bs"
import "pkg:/source/services/InvidiousService.bs"
import "pkg:/source/roku_modules/rokurequests/Requests.brs"

namespace Http

    class InvidiousRouter extends HttpRouter

        function new()
            super()

            m.Get(Invidious.TOKEN_CALLBACK, function(context as object) as boolean
                request = context.request
                response = context.response
                server = context.server

                if request.query["token"] <> invalid and request.query["ref"] <> invalid
                    ' Token is encoded twice for some reason
                    token = request.query["token"].DecodeUri().DecodeUriComponent()
                    referer = request.query["ref"].DecodeUri()
                    username = request.query["username"]
                    ' TODO: remove null check after 6 months of https://github.com/iv-org/invidious/pull/3603
                    if username <> invalid
                        username = username.DecodeUriComponent().DecodeUriComponent()
                    end if

                    InvidiousSettings.SetAuthToken(token, referer, username)
                    server.task.top.login = true
                end if
                response.Redirect("/")
                return true
            end function)

            m.Get("/invidious/authenticated-request", function(context as object) as boolean
                request = context.request
                response = context.response

                requestData = request.query["request-data"]
                if requestData = invalid
                    response.Default(400, "Expected request-data")
                    return true
                end if

                token = InvidiousSettings.GetAuthToken()
                if token = invalid
                    response.Default(403, "No token, not logged in")
                    return true
                end if

                requestData = ParseJson(requestData)
                ' Format object again because FormatJson formats in a specific way (sorts keys, etc.) 
                ' so we want an exact string for the md5 hash to match
                requestDataJson = FormatJson(requestData)
                cacheKey = GetMd5(requestDataJson)
                path = `tmp:/${cacheKey}.json`

                fs = CreateObject("roFilesystem")
                if fs.exists(path)
                    cacheData = ReadAsciiFile(path)
                    response.SetBodyDataString(cacheData)
                    response.ContentType("application/json")
                    response.source = Http.HttpResponseSource.GENERATED
                    return true
                end if

                invService = new InvidiousService()
                result = invService.MakeRequest(requestData)
                if result = invalid
                    response.Default(500, "Failed to make request")
                    return true
                end if
                response.Json(result)
                return true
            end function)

        end function

    end class

end namespace
