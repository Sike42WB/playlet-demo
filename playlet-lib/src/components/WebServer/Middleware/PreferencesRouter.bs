namespace Http

    class PreferencesRouter extends HttpRouter

        function new()
            super()

            ' Get preferences
            m.Get("/api/preferences", function(context as object) as boolean
                response = context.response
                task = context.server.task
                scene = task.top.getScene()
                settings = scene.findNode("SettingsScreen")
                if settings = invalid
                    response.Default(500, `Settings screen not found: scene.findNode("SettingsScreen") returned invalid`)
                    return true
                end if

                ' Passing invalid because of call limitation
                preferences = settings@.GetUserPreferences(invalid)
                response.Json(preferences)
                return true
            end function)

            ' Update preferences
            m.Put("/api/preferences", function(context as object) as boolean
                request = context.request
                response = context.response
                task = context.server.task

                payload = request.Json()
                if payload = invalid
                    response.Default(400, `Invalid JSON payload`)
                    return true
                end if

                scene = task.top.getScene()
                settings = scene.findNode("SettingsScreen")
                if settings = invalid
                    response.Default(500, `Settings screen not found: scene.findNode("SettingsScreen") returned invalid`)
                    return true
                end if

                settings@.ApplyUserPreferences(payload)
                response.Default(204, "OK")
                return true
            end function)

            ' Reset default preferences
            m.Delete("/api/preferences", function(context as object) as boolean
                response = context.response
                task = context.server.task

                scene = task.top.getScene()
                settings = scene.findNode("SettingsScreen")
                if settings = invalid
                    response.Default(500, `Settings screen not found: scene.findNode("SettingsScreen") returned invalid`)
                    return true
                end if

                settings@.ClearUserPreferences(invalid)
                response.Default(204, "OK")
                return true
            end function)

        end function

    end class

end namespace
