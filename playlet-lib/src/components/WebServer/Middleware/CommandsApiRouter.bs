namespace Http

    class CommandsApiRouter extends HttpRouter

        function new()
            super()

            m.Post("/api/command", function(context as object) as boolean
                request = context.request
                response = context.response
                server = context.server

                payload = request.Json()
                if payload = invalid
                    response.Default(400, "Invalid payload")
                    return true
                end if

                if payload.command = "logout"
                    if Invidious.Logout()
                        server.task.top.login = false
                    end if
                    response.Default(204, "OK")
                    return true
                else if payload.command = "play"
                    server.task.top.command = payload
                    response.Default(204, "OK")
                    return true
                else if payload.command = "set-playlet-lib-urls"
                    content = payload.content
                    if content = invalid
                        RegistryUtils.Delete(RegistryUtils.PLAYLET_LIB_URLS)
                    else
                        RegistryUtils.Write(RegistryUtils.PLAYLET_LIB_URLS, content)
                    end if
                    response.Default(204, "OK")
                    return true
                end if

                response.Default(400, `Invalid command: ${payload.command}`)
                return true
            end function)
        end function

    end class

end namespace
