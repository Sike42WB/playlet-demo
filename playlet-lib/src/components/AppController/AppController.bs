import "pkg:/source/utils/StringUtils.bs"
import "pkg:/source/utils/NodeSetFocus.bs"

function Init()
    m.screens = {}
end function

function OnNodeReady()
    NodeSetFocus(m.top.findNode("NavBar"), true)
end function

function PushScreen(node as object)
    m.root.appendChild(node)
    FocusTopScreen()
end function

function PopScreen(unused as dynamic)
    childCount = m.root.getChildCount()
    if childCount > 1
        m.root.removeChildIndex(childCount - 1)
        FocusTopScreen()
    end if
end function

function FocusTopScreen()
    childCount = m.root.getChildCount()
    topScreen = m.root.getChild(childCount - 1)
    NodeSetFocus(topScreen, true)
end function

function HideRootScreen(screenName as string) as void
    if StringUtils.IsNullOrEmpty(screenName)
        return
    end if
    if not m.screens.DoesExist(screenName)
        return
    end if
    screen = m.screens[screenName]
    if screen = invalid
        return
    end if
    screen.visible = false
end function

function ShowRootScreen(screenName as string) as void
    if StringUtils.IsNullOrEmpty(screenName)
        return
    end if
    if not m.screens.DoesExist(screenName)
        screen = CreateObject("roSGNode", screenName)
        screen.id = screenName
        m.screens[screenName] = screen
        m.screensNode.appendChild(screen)
        if screen.hasField("binding_done")
            screen@.BindNode(invalid)
        end if
    end if
    screen = m.screens[screenName]
    if screen = invalid
        return
    end if
    screen.visible = true
end function

function GetRootScreen(screenName as string) as object
    if StringUtils.IsNullOrEmpty(screenName)
        return invalid
    end if
    if not m.screens.DoesExist(screenName)
        return invalid
    end if
    return m.screens[screenName]
end function
