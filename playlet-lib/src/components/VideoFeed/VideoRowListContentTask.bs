import "FeedLoadState.bs"

@asynctask
function VideoRowListContentTask(input as object)
    contentData = input.contentData
    rowList = input.node

    contentNode = CreateObject("roSGNode", "ContentNode")

    for each feed in contentData
        feedContentNode = CreateObject("roSGNode", "ContentNode")
        feedContentNode.AddField("feed", "assocarray", false)
        feedContentNode.AddField("loadState", "string", false)

        feedContentNode.feed = feed
        feedContentNode.loadState = FeedLoadState.None

        feedContentNode.title = feed.title

        for i = 1 to 4
            placeHolder = feedContentNode.createChild("ContentNode")
            placeHolder.AddField("isPlaceHolder", "boolean", false)
            placeHolder.isPlaceHolder = true
        end for

        contentNode.appendChild(feedContentNode)
    end for

    rowList.content = contentNode
end function
