function Init()
    m.authorLabel = m.top.FindNode("authorLabel")
    m.subCountLabel = m.top.FindNode("subCountLabel")
    m.videoCountLabel = m.top.FindNode("videoCountLabel")
    m.thumbnail = m.top.FindNode("thumbnail")
    m.thumbnail.failedBitmapUri = "pkg:/images/thumbnail-missing.jpg"
end function

function OnContentSet() as void
    content = m.top.itemContent

    if content = invalid
        return
    end if

    m.authorLabel.text = content.author
    SetSubCount(content.subCount)

    m.thumbnail.uri = GetThumbnailUrl(content.authorThumbnails)

    SetVideoCount(content.videoCount)
end function

function SetSubCount(subCount as integer)
    subCountText = ""
    if subCount = 1
        subCountText = "1 subscriber"
    else
        subCountText = `${subCount} subscribers`
    end if

    m.subCountLabel.text = subCountText
end function


function SetVideoCount(videoCount as integer)
    videoCountText = ""
    if videoCount = 1
        videoCountText = "1 video"
    else
        videoCountText = `${videoCount} videos`
    end if

    m.videoCountLabel.text = videoCountText
end function

function GetThumbnailUrl(videoThumbnails as object) as string
    if videoThumbnails = invalid or videoThumbnails.Count() = 0
        return ""
    end if
    url = videoThumbnails[videoThumbnails.Count() - 1].url
    if url.startsWith("//")
        url = "https:" + url
    end if
    return url
end function
