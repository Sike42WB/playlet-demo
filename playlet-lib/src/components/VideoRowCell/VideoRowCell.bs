import "pkg:/source/utils/TimeUtils.bs"

function OnContentSet() as void
    content = m.top.itemContent

    if content = invalid
        return
    end if

    m.top.FindNode("titleLabel").text = content.title
    m.top.FindNode("authorLabel").text = content.author

    if IsVideoUpcoming(content)
        m.top.FindNode("viewCountDateLabel").text = `Premeres in ${TimeUtils.GetFormattedTimeLeft(content.premiereTimestamp)}`
    else
        publishedText = validstr(content.publishedText)
        viewCount = FormatViewCount(content.viewCount)
        if publishedText = "" and viewCount = ""
            m.top.FindNode("viewCountDateLabel").text = ""
        else
            m.top.FindNode("viewCountDateLabel").text = `${publishedText} â€¢ ${viewCount}`
        end if
    end if

    m.thumbnail = m.top.FindNode("thumbnail")
    m.thumbnail.observeField("loadStatus", "OnThumbnailLoadFailed")
    m.thumbnail.uri = GetThumbnailUrl(content.videoThumbnails)
    m.thumbnail.id = `thumbnail-${content.videoId}`

    if IsVideoLive(content)
        m.top.FindNode("liveRect").visible = true
        m.top.FindNode("durationRect").visible = false
    else
        m.top.FindNode("liveRect").visible = false
        if content.lengthSeconds <> invalid and content.lengthSeconds <> 0
            m.top.FindNode("durationLabel").text = TimeUtils.GetFormattedTime(content.lengthSeconds)
            m.top.FindNode("durationRect").visible = true
        else
            m.top.FindNode("durationRect").visible = false
        end if
    end if

end function

function OnThumbnailLoadFailed() as void
    if m.thumbnail.loadStatus <> "failed"
        return
    end if
    m.thumbnail.uri = "pkg:/images/thumbnail-missing.jpg"
end function

function GetThumbnailUrl(videoThumbnails as object, quality = "medium" as string) as string
    if videoThumbnails.Count() = 0
        return ""
    end if
    for each thumbnail in videoThumbnails
        if thumbnail.quality = quality
            return thumbnail.url
        end if
    end for
    return videoThumbnails[0].url
end function

function IsVideoUpcoming(metadata as object) as boolean
    return metadata.isUpcoming = true
end function

function IsVideoLive(metadata as object) as boolean
    if metadata.liveNow = true
        return true
    end if
    ' TODO: metadata.liveNow is returning false for live videos...
    return metadata.lengthSeconds = 0 and metadata.viewCount = 0
end function

function FormatViewCount(viewCount as dynamic) as string
    if viewCount = invalid
        return ""
    end if
    if viewCount < 1000
        return `${FormatFloat(viewCount)} views`
    end if

    viewCount = viewCount / 1000
    if viewCount < 1000
        return `${FormatFloat(viewCount)}K views`
    end if

    viewCount = viewCount / 1000
    if viewCount < 1000
        return `${FormatFloat(viewCount)}M views`
    end if

    viewCount = viewCount / 1000
    return `${FormatFloat(viewCount)}B views`
end function

function FormatFloat(X as integer) as integer
    X = X * 10
    X = X + 0.5
    X = Fix(X)
    X = X / 10
    return X
end function
