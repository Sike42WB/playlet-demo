import "pkg:/source/utils/RemoteKeys.bs"

namespace Tests

    @SGNode("NavigationTestNode")
    @suite
    class NavigationTests extends Rooibos.BaseTestSuite

        @describe("Navigation")

        @afterEach
        function _()
            m.node._navigation = invalid
        end function

        @it("save data of the navigation")
        function _()
            node1 = CreateObject("roSGNode", "Node")
            node2 = CreateObject("roSGNode", "Node")

            m.top@.SetNavigation(node1, RemoteKeys.Right, node2)

            m.assertAAHasKey(m.node._navigation, RemoteKeys.Right)
        end function

        @it("navigates to the right node")
        function _()
            node1 = CreateObject("roSGNode", "Node")
            node2 = CreateObject("roSGNode", "Node")

            m.top@.SetNavigation(node1, RemoteKeys.Right, node2)

            NodeSetFocus(node1, true)
            node2.focusable = true

            m.top@.NavigationKeyHandler(RemoteKeys.Right, true)

            m.assertFalse(node1.hasFocus())
            m.assertTrue(node2.hasFocus())
        end function

        @it("navigates to the right node from anywhere")
        function _()
            node2 = CreateObject("roSGNode", "Node")

            m.top@.SetNavigation(invalid, RemoteKeys.Right, node2)

            node2.focusable = true

            m.top@.NavigationKeyHandler(RemoteKeys.Right, true)

            m.assertTrue(node2.hasFocus())
        end function

    end class

end namespace
