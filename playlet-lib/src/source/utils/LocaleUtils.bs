' TODO:P2 Translations left:
'   - Context menu
'   - Search filters
'   - Loading messages
' Web app:
' - Video publish text
' - Video video count
' - Video live/upcoming/premiere indicator
' - Playlist updated time
' - Playlist video count
' - Channel sub count

' - Video dialog options
'   - Play on
'   - Queue on
'   - Open in Invidious
'   - Cancel

' - Channel dialog options
'   - Open on
'   - Open in Invidious
'   - Cancel

' - Playlist dialog options
'   - Play on
'   - Queue on
'   - Open on
'   - Open in Invidious
'   - Cancel

import "pkg:/source/utils/MathUtils.bs"

namespace LocaleUtils

    function GetPluralString(c as integer, zeroString as string, oneString as string, pluralString as string) as string
        if c = 0
            return Tr(zeroString)
        else if c = 1
            return Tr(oneString)
        else
            return Tr(pluralString).Replace("^n", c.toStr())
        end if
    end function

    function GetFormattedPluralString(c as integer, zeroString as string, oneString as string, pluralString as string) as string
        if c = 0
            return Tr(zeroString)
        else if c = 1
            return Tr(oneString)
        else
            if c < 1000
                return Tr(pluralString).Replace("^n", MathUtils.FormatFloat(c).toStr())
            end if
            c = c / 1000
            if c < 1000
                return Tr(pluralString).Replace("^n", MathUtils.FormatFloat(c).toStr() + "K")
            end if

            c = c / 1000
            if c < 1000
                return Tr(pluralString).Replace("^n", MathUtils.FormatFloat(c).toStr() + "M")
            end if

            c = c / 1000
            return Tr(pluralString).Replace("^n", MathUtils.FormatFloat(c).toStr() + "B")
        end if
    end function

end namespace
