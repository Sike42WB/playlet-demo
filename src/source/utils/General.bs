function GetLocalIpAddress() as string
    if m.ipAddress = invalid or m.ipAddress = ""
        di = CreateObject("roDeviceInfo")
        ips = di.GetIPAddrs()
        for each ip in ips
            m.ipAddress = ips[ip]
            return m.ipAddress
        end for
    end if
    return m.ipAddress
end function

function GetUserCountryCode() as string
    if m.countryCode = invalid or m.countryCode = ""
        m.countryCode = CreateObject("roDeviceInfo").GetUserCountryCode()
    end if
    return m.countryCode
end function

function GetAppInfo() as object
    if m.appInfo = invalid
        appInfo = CreateObject("roAppInfo")
        m.appInfo = {
            id: appInfo.GetID(),
            version: appInfo.GetVersion()
        }
    end if
    return m.appInfo
end function

function GetDeviceInfo() as object
    if m.deviceInfo = invalid
        devInfo = CreateObject("roDeviceInfo")
        m.deviceInfo = {
            model: devInfo.GetModel(),
            model_display_name: devInfo.GetModelDisplayName(),
            model_type: devInfo.GetModelType(),
            model_details: devInfo.GetModelDetails(),
            os_version: devInfo.GetOSVersion(),
            display_type: devInfo.GetDisplayType(),
            display_mode: devInfo.GetDisplayMode(),
            display_aspect_ratio: devInfo.GetDisplayAspectRatio(),
            display_size: devInfo.GetDisplaySize(),
            video_mode: devInfo.GetVideoMode(),
            ui_resolution: devInfo.GetUIResolution(),
            graphics_platform: devInfo.GetGraphicsPlatform(),
            internet_status: devInfo.GetInternetStatus()
        }
    end if
    return m.deviceInfo
end function

function ArrayIndexOf(array as object, value as string) as integer
    for i = 0 to array.Count() - 1
        if array[i] = value
            return i
        end if
    end for
    return -1
end function

function ArrayRemoveAt(array as object, index as integer) as object
    result = []
    for i = 0 to array.Count() - 1
        if i <> index
            result.push(array[i])
        end if
    end for
    return result
end function
