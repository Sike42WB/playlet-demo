REM This file has no dependencies on other common files.
REM
REM Functions in this file:
REM     GetDeviceVersion
REM     GetDeviceESN
REM     IsHD
REM     GetLocalIpAddress
REM     GetUserCountryCode
REM

'******************************************************
'Get our device version
'******************************************************
function GetDeviceVersion() as string
    if m.softwareVersion = invalid or m.softwareVersion = ""
        m.softwareVersion = CreateObject("roDeviceInfo").GetVersion()
    end if
    return m.softwareVersion
end function


'******************************************************
'Get our serial number
'******************************************************
function GetDeviceESN() as string
    if m.serialNumber = invalid or m.serialNumber = ""
        m.serialNumber = CreateObject("roDeviceInfo").GetDeviceUniqueId()
    end if
    return m.serialNumber
end function


'******************************************************
'Determine if the UI is displayed in SD or HD mode
'******************************************************
function IsHD() as boolean
    di = CreateObject("roDeviceInfo")
    if di.GetDisplayMode() = "720p"
        return true
    end if
    return false
end function

function GetLocalIpAddress() as string
    if m.ipAddress = invalid or m.ipAddress = ""
        di = CreateObject("roDeviceInfo")
        ips = di.GetIPAddrs()
        for each ip in ips
            m.ipAddress = ips[ip]
            return m.ipAddress
        end for
    end if
    return m.ipAddress
end function

function GetUserCountryCode() as string
    if m.countryCode = invalid or m.countryCode = ""
        m.countryCode = CreateObject("roDeviceInfo").GetUserCountryCode()
    end if
    return m.countryCode
end function
