function main(args as object) as void
    screen = CreateObject("roSGScreen")
    m.port = CreateObject("roMessagePort")
    screen.setMessagePort(m.port)
    m.global = screen.getGlobalNode()

    scene = screen.CreateScene("MainScene")
    screen.show()
    scene.ObserveField("exitChannel", m.port)
    scene.launchArgs = args

    ' The following comment is to enable the SceneGraph inspector
    ' on the VSCode BrightScript plugin.
    #if DEBUG
        ' vscode_rdb_on_device_component_entry
    #end if

    while true
        msg = wait(0, m.port)
        msgType = type(msg)
        if msgType = "roSGScreenEvent"
            if msg.isScreenClosed()
                return
            end if
        else if msgType = "roSGNodeEvent"
            field = msg.getField()
            data = msg.getData()
            if field = "exitChannel" and data = true
                END
            end if
        else if msgType = "roInputEvent"
            scene.inputArgs = msg.getInfo()
        end if
    end while
end function
