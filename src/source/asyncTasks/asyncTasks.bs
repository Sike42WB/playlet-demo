function StartAsyncTask(taskFunc as object, input as object, callback = invalid as object) as object
    task = CreateAsyncTask(taskFunc, input, callback)
    task.control = "RUN"
    return task
end function

function CreateAsyncTask(taskFunc as object, input as object, callback = invalid as object) as object
    id = StrI(rnd(2147483647), 36)
    task = createObject("roSGNode", GetFunctionName(taskFunc))
    task.id = id
    task.input = input
    if callback <> invalid
        m[`asynctask_${id}`] = task
        m[`asynctask_${id}_callback`] = callback
        task.ObserveField("output", "TaskCallback")
    end if
    return task
end function

function GetFunctionName(taskFunc as object) as string
    return (LCase(`${taskFunc}_asynctask`)).replace("function:", "").trim()
end function

function TaskCallback(e as object)
    task = e.getRoSGNode()
    id = task.id
    callback = m[`asynctask_${id}_callback`]

    output = task.output
    callback(output)

    m.delete(`asynctask_${id}`)
    m.delete(`asynctask_${id}_callback`)
end function
