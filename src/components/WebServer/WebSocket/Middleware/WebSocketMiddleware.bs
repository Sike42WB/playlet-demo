import "pkg:/source/utils/Crypto.bs"

namespace Http

    const WEB_SOCKET_MAGIC_STRING = "258EAFA5-E914-47DA-95CA-C5AB0DC85B11"

    class WebSocketMiddleware extends HttpRouter

        function new()
            super()

            m.Get("*", function(context as object) as boolean
                request = context.request
                response = context.response
                connection = context.connection

                if LCase(validStr(request.headers["Connection"])) <> "upgrade"
                    return false
                end if

                if LCase(validStr(request.headers["Upgrade"])) <> "websocket"
                    return false
                end if

                webSocketKey = request.headers["Sec-WebSocket-Key"]
                if webSocketKey = invalid
                    return false
                end if

                sha1Hash = GetSha1(webSocketKey + Http.WEB_SOCKET_MAGIC_STRING)
                buffer = CreateObject("roByteArray")
                buffer.FromHexString(sha1Hash)
                webSocketAccept = ToBase64(buffer.ToAsciiString())

                response.http_code = 101
                response.headers["Connection"] = "Upgrade"
                response.headers["Upgrade"] = "websocket"
                response.headers["Sec-WebSocket-Accept"] = webSocketAccept
                response.header_only = true
                response.source = Http.HttpResponseSource.GENERATED

                connection.ws_upgrade = true

                return true
            end function)
        end function

    end class

end namespace
