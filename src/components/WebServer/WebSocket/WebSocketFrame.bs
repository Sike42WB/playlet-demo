import "pkg:/components/WebServer/WebSocket/WebSocketOpCode.bs"

namespace Web

    class WebSocketFrame
        public fin
        public opcode
        public is_masked
        public payload_size
        public mask
        public payload

        private index
        private length

        function new()
            m.payload = CreateObject("roByteArray")
        end function

        function ReadFromBuffer(buffer as object, index as integer, length as integer) as boolean
            m.buffer = buffer
            m.index = index
            m.length = length

            firstByte = m.ReadByte()
            m.fin = (firstByte and &h80) <> 0
            m.opcode = firstByte and &hf

            if not m.fin
                return false
            end if

            if m.opcode <> Web.WebSocketOpCode.TEXT
                return false
            end if

            secondByte = m.ReadByte()
            m.is_masked = (secondByte and &h80) <> 0
            m.payload_size = secondByte and &h7F

            if not m.is_masked
                return false
            end if

            if m.payload_size >= 126
                return false
            end if

            m.mask = [m.ReadByte(), m.ReadByte(), m.ReadByte(), m.ReadByte()]

            m.payload.Clear()
            for i = 0 to m.payload_size - 1
                m.payload[i] = m.Xor(m.ReadByte(), m.mask[i mod 4])
            end for

            return true
        end function

        function WriteToBuffer(buffer as object, index as integer, length as integer) as integer
            m.buffer = buffer
            m.index = index
            m.length = length

            m.WriteByte(m.fin or m.opcode)
            m.WriteByte(m.payload_size)

            for i = 0 to m.payload_size - 1
                m.buffer[m.index] = m.payload[i]
                m.index++
            end for

            return m.index - index
        end function

        function GetPayloadText() as string
            return m.payload.ToAsciiString()
        end function

        function SetPayloadText(text as string)
            m.fin = &h80
            m.opcode = Web.WebSocketOpCode.TEXT
            m.is_masked = false
            m.mask = invalid
            m.payload.FromAsciiString(text)
            m.payload_size = m.payload.Count()
        end function

        function ReadByte() as integer
            byte = m.buffer[m.index]
            m.index++
            return byte
        end function

        function WriteByte(byte as integer) as integer
            m.buffer[m.index] = byte
            m.index++
            return byte
        end function

        function Xor(a as integer, b as integer) as integer
            return (a or b) and not (a and b)
        end function
    end class

end namespace
