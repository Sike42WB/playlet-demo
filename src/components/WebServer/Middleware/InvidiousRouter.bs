namespace Http

    class InvidiousRouter extends HttpRouter

        function new()
            super()

            ' TODO: use POST once supported
            m.Get("/invidious/set-instances", function(context as object) as boolean
                request = context.request
                response = context.response

                instances = []
                if request.query.DoesExist("instances")
                    instances = request.query["instances"]
                    if GetInterface(instances, "ifArray") = invalid
                        instances = [instances]
                    end if
                end if

                for i = 0 to instances.Count() - 1
                    if instances[i].right(1) = "/"
                        instances[i] = instances[i].left(instances[i].len() - 1)
                    end if
                end for

                InvidiousSettings.SetRegistryInstances(instances)
                response.Default(200, "OK")
                return true
            end function)

            m.Get(Invidious.TOKEN_CALLBACK, function(context as object) as boolean
                request = context.request
                response = context.response
                server = context.server

                if request.query["token"] <> invalid and request.query["referer"] <> invalid
                    ' Token is encoded twice for some reason
                    token = WebUtils.UrlUnescape(request.query["token"]).DecodeUriComponent()
                    referer = WebUtils.UrlUnescape(request.query["referer"])
                    InvidiousSettings.SetAuthToken(token, referer)
                    server.task.top.login = true
                end if
                response.Redirect("/", 302)
                return true
            end function)
        end function

    end class

end namespace
