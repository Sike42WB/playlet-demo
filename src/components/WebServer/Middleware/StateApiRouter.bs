import "pkg:/source/services/Invidious.bs"
import "pkg:/source/utils/General.bs"

namespace Http

    class StateApiRouter extends HttpRouter

        function new()
            super()

            m.Get("/api/state", function(context as object) as boolean
                response = context.response

                authToken = InvidiousSettings.GetAuthToken()
                state = {
                    app: GetAppInfo(),
                    device: GetDeviceInfo(),
                    invidious: {
                        instances: InvidiousSettings.GetCurrentInstances(),
                        selected_instance: InvidiousSettings.GetSelectedInstance(),
                        auth_url: Invidious.GetAuthorizeTokenLink(),
                        logged_in: authToken <> invalid,
                        logged_in_instance: authToken <> invalid ? authToken.instance : invalid
                    }
                }
                response.Json(state)
                return true
            end function)
        end function

    end class

end namespace
