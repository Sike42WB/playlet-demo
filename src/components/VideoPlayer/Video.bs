import "pkg:/source/services/Invidious.bs"
import "pkg:/components/Dialog/DialogUtils.bs"
import "pkg:/source/roku_modules/rokupromise/Promise.brs"

function UtilsPlayVideo(videoId as string, sender = invalid as object)
    UtilsCloseVideo(false)
    videoContainer = UtilsGetVideoContainer()
    if sender <> invalid
        videoContainer.sender = sender
    end if
    videoContainer.videoPlayer = videoContainer.createChild("VideoPlayer")
    videoContainer.videoPlayer.videoId = videoId
    videoContainer.videoPlayer.SetFocus(true)
end function

function UtilsCloseVideo(setFocus = true as boolean)
    videoContainer = UtilsGetVideoContainer()
    if videoContainer.videoPlayer <> invalid
        videoContainer.videoPlayer.control = "stop"
        videoContainer.RemoveChild(videoContainer.videoPlayer)
        videoContainer.videoPlayer = invalid
    end if
    if setFocus and videoContainer.sender <> invalid
        if videoContainer.sender.hasField("focus")
            videoContainer.sender.focus = true
        else
            videoContainer.sender.SetFocus(true)
        end if
    end if
end function

function UtilsGetVideoContainer() as object
    return m.global.videoContainer
end function
