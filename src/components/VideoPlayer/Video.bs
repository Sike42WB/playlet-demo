import "pkg:/source/services/Invidious.bs"
import "pkg:/components/Dialog/DialogUtils.bs"
import "pkg:/source/roku_modules/rokupromise/Promise.brs"

function PlayVideo(videoId as string, sender = invalid as object)
    CloseVideo(false)
    videoContainer = GetVideoContainer()
    if sender <> invalid
        videoContainer.sender = sender
    end if
    videoContainer.videoPlayer = videoContainer.createChild("VideoPlayer")
    videoContainer.videoPlayer.videoId = videoId
    if videoContainer.fullscreen
        videoContainer.videoPlayer.SetFocus(true)
    end if
end function

function CloseVideo(setFocus = true as boolean)
    videoContainer = GetVideoContainer()
    if videoContainer.videoPlayer <> invalid
        videoContainer.videoPlayer.control = "stop"
        videoContainer.RemoveChild(videoContainer.videoPlayer)
        videoContainer.videoPlayer = invalid
    end if
    if setFocus and videoContainer.sender <> invalid
        if videoContainer.sender.hasField("focus")
            videoContainer.sender.focus = true
        else
            videoContainer.sender.SetFocus(true)
        end if
    end if
    m.global.loadingIndicator.visible = false
end function

function ToggleVideoPictureInPicture(sender = invalid as object) as boolean
    videoContainer = GetVideoContainer()
    if videoContainer.videoPlayer = invalid
        return false
    end if
    videoContainer.fullscreen = not videoContainer.fullscreen
    if sender <> invalid
        videoContainer.sender = sender
    end if
    if videoContainer.fullscreen
        videoContainer.videoPlayer.SetFocus(true)
    else
        if videoContainer.sender <> invalid
            if videoContainer.sender.hasField("focus")
                videoContainer.sender.focus = true
            else
                videoContainer.sender.SetFocus(true)
            end if
        end if
        videoContainer.sender = invalid
    end if
    return true
end function

function GetVideoContainer() as object
    return m.global.videoContainer
end function
